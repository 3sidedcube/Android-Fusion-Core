// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
	repositories {
		google()
		mavenCentral()
	}
	dependencies {
		classpath "com.android.tools.build:gradle:7.0.3"
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.0"

		// NOTE: Do not place your application dependencies here; they belong
		// in the individual module build.gradle files
	}
}

/**
 * Gets the version of exported artifacts, ensuring that (if building a pre-release or release build with Jitpack) it matches this version
 *
 * @param requiredVersion the version that is required for this project
 * @return the consistent required version identifier for exported artifacts
 */
def thisOrJitpackVersion(requiredVersion) {
	if("$System.env.JITPACK".toString() == "true") {
		def jitpackVersion = "$System.env.VERSION".toString()
		if(jitpackVersion ==~ /\w?\d+\.\d+\.\d+(-\w+)?/)
		{
			// A pre-release or release version: change the lib version to match
			return jitpackVersion
		}
	}
	return requiredVersion
}

ext {
	// Do not remove the call to thisOrJitpackVersion - this ensures that versions for all generated artifacts are aligned.
	fusionLibVersion = thisOrJitpackVersion('0.0.1')
}

task clean(type: Delete) {
	delete rootProject.buildDir
}